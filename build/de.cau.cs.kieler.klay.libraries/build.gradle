
// TODO turn this into a gradle wrapper ?!

/*
 *
 */

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'de.cau.cs.kieler'
version = '1.3.37'
ext.packaging = 'jar'

sourceSets {
    main {
        java {
            srcDir '../../plugins/de.cau.cs.kieler.core/src'
            srcDir '../../plugins/de.cau.cs.kieler.core.kgraph/src'
            srcDir '../../plugins/de.cau.cs.kieler.kiml/src'
            srcDir '../../plugins/de.cau.cs.kieler.klay.force/src'
            srcDir '../../plugins/de.cau.cs.kieler.klay.layered/src'
            srcDir '../../plugins/de.cau.cs.kieler.klay.tree/src'
        }
        resources {
            srcDir '../../plugins/de.cau.cs.kieler.kiml/images'
            srcDir '../../plugins/de.cau.cs.kieler.klay.force/images'
            srcDir '../../plugins/de.cau.cs.kieler.klay.layered/images'
            srcDir '../../plugins/de.cau.cs.kieler.klay.tree/images'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.eclipse.emf:org.eclipse.emf.common:[0,)'
    compile 'org.eclipse.emf:org.eclipse.emf.ecore:[0,)'
    compile 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:[0,)'
    compile 'com.google.guava:guava-gwt:[0,)'
}


task sourcesJar (type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadoc (type: Javadoc, overwrite: true) {
    source = sourceSets.main.allJava
    failOnError = false
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "file://$buildDir/repo") {
              // authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'KlayBundle'
                packaging 'jar'
                description 'Bundled Standalone Klay'
                url 'http://rtsys.informatik.uni-kiel.de'

                licenses {
                    license {
                        name 'Eclipse Public License - v 1.0'
                        url 'http://www.eclipse.org/org/documents/epl-v10.html'
                    }
                }
            }
        }
    }
}
