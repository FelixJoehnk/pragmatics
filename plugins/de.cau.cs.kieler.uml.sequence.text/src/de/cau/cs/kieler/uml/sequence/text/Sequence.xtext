/*
 * KIELER - Kiel Integrated Environment for Layout Eclipse RichClient
 *
 * http://www.informatik.uni-kiel.de/rtsys/kieler/
 * 
 * Copyright 2015 by
 * + Kiel University
 *   + Department of Computer Science
 *     + Real-Time and Embedded Systems Group
 * 
 * This code is provided under the terms of the Eclipse Public License (EPL).
 * See the file epl-v10.html for the license text.
 */
grammar de.cau.cs.kieler.uml.sequence.text.Sequence with org.eclipse.xtext.common.Terminals

generate sequence "http://www.cau.de/cs/kieler/uml/sequence/text/Sequence"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SequenceDiagram returns SequenceDiagram:
    {SequenceDiagram}
    'sequenceDiagram' diagramName=STRING ('{' locals+=LocalVariable (locals+=LocalVariable)* '}')?
    lifelines+=Lifeline*
    interactions+=Interaction*;

LocalVariable returns LocalVariable:
    type=DataType name=ID;

Lifeline returns Lifeline:
    'lifeline' caption=STRING 'as' name=ID;

Interaction returns Interaction:
    TwoLifelineMessage | OneLifelineMessage | Fragment | OneLifelineEndBlock | OneLifelineNote
    | DestroyLifelineEvent | Refinement;

TwoLifelineMessage returns TwoLifelineMessage:
    sourceLifeline=[Lifeline|ID]
    messageType=MessageType
    message=STRING
    'to'
    targetLifeline=[Lifeline|ID]
    (sourceStartBlock?='sourceStartBlock' | sourceEndBlock?='sourceEndBlock' (sourceEndBlockCount=INT_GREATER_ZERO)?)?
    (targetStartBlock?='targetStartBlock' | targetEndBlock?='targetEndBlock' (targetEndBlockCount=INT_GREATER_ZERO)?)?
    ('sourceNote' sourceNote=STRING)?
    ('targetNote' targetNote=STRING)?;

OneLifelineMessage returns OneLifelineMessage:
    Lifeline=[Lifeline|ID]
    messageType=MessageType
    messageTypeLostAndFound=('lost' | 'found')
    caption=STRING
    (startBlock?='startBlock' | endBlock?='endBlock' (endBlockCount=INT_GREATER_ZERO)?)?
    ('note' note=STRING)?;

OneLifelineEndBlock returns OneLifelineEndBlock:
    Lifeline=[Lifeline|ID]
    'endBlock' (endBlockCount=INT_GREATER_ZERO)?;

OneLifelineNote returns OneLifelineNote:
    Lifeline=[Lifeline|ID]
    'note' note=STRING;

DestroyLifelineEvent returns DestroyLifelineEvent:
    Lifeline=[Lifeline|ID]
    'destroy';

Fragment returns Fragment:
    'fragment'
    name=STRING
    sections+=Section (sections+=Section)*;

Section returns Section:
    '{'
    ('label' label=STRING)?
    interactions+=Interaction (interactions+=Interaction)*
    '}';

Refinement returns Refinement:
    'refinement'
    'label' label=STRING
    'lifelines' lifelines+=[Lifeline|ID] (',' lifelines+=[Lifeline|ID])*;

enum MessageType:
    async='async' | create='create' | response='response' | sync='sync';

enum DataType:
    char='Char' | boolean='Boolean' | integer='Integer' | float='Float';

terminal INT_GREATER_ZERO returns ecore::EInt:
    '1'..'9' ('0'..'9')* | 'all';